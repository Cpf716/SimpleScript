//  Author: Corey Ferguson
//  Date:   September 22, 2023
//  File:   sender.txt
//

include("/Library/Application Support/SimpleScript/example/socket/tcp/util/server.txt")

//  FUNCTIONS

cfdc = 0

func onExit(_)
    if cfdc
        echo "\n"
        echo "Are you sure? (y/N) "
    
        const ans = (input() ?? "")
    
        return toLower ans === "y"
    end if
    
    return true
end func

func main()
    sfd = server(1024)
    
    array cfdv = null

    while 1
        if !(cfdv = poll(sfd))
            if cfdc
                cfdc = 0
                echo "Disconnected.\t(0)\n"
            end if
        
            continue
        end if
        
        if cfdc < count cfdv
            echo "Connected...\t({})\n" format count cfdv
        else if cfdc > count cfdv
            echo "Disconnected.\t({})\n" format count cfdv
        end if
        
        cfdc = count cfdv
        
        for cfd in cfdv
            try
                send(cfd, gmt())
            catch errno
                try
                    closeTcp(cfd)
                catch errno
                    throw ERRNOs().errno
                end try
                
                if ERRNOs().errno !== "EPIPE"
                    echo ERRNOs().errno
                end if
            end try
        end for
        
        sleep 1_000
    end while
end func

main()
