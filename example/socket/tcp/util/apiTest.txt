//  Author: Corey Ferguson
//  Date:   April 25, 2024
//  File:   apiTest.txt
//

include((dir = "/Library/Application Support/SimpleScript/example/socket/") + "tcp/util/client.txt")
include(dir + "config.txt")

config()

assert typeOf PORT !== undefined

//  FUNCTIONS

func onExit(_)
    echo "\n\n"
    echo "Press return to continue...\n"
    
    return true
end func

func main()
    if nrows(argv) >= 2
        expectExcept("1 argument(s), got {}" format nrows(argv))
    end if

    array port = PORT

    if nrows(argv) === 1
        port = (argv row 0) slice 1
        
        if subtypeOf array(...port) !== "int"
            typeExcept(subtypeOf array(...port), "int")
        end if
        
        if port < 0
            rangeExcept(string(port))
        end if
    end if
    
    echo "\n"
    
    while true
        echo "Req: "
        
        req = null
        while true
            req = string(input() ?? "")
            
            if req
                break
            end if
        end while
        
        echo "\n"

        sfd = client(port)

        if sfd === -1
            echo "Connection to the server timed out.\n\n"
            echo "Press return to continue..."
            
            input()
            
            echo "\n"
            
            closeTcp(sfd)
            continue
        end if
        
        
        
        send(sfd, req)
        
        try
            echo recv(sfd, 30) + "\n"
        catch errno
            echo ERRNOs().errno + "\n"
        end try
        
        try
            closeTcp(sfd)
        catch errno
            throw ERRNOs().errno
        end try
        
        echo "\n"
    end while
end func

main()
