//  Author: Corey Ferguson
//  Date:   November 16, 2023
//  File:   receiver.txt
//

include("@/array/table/parseTable")
include((dir = "/Library/Application Support/SimpleScript/example/socket/") + "util/client.txt", connectClient)
include(dir + "config.txt")
include((dir = "@/number/") + "padDecimal")
include(dir + "precision")
include("@/string")

config()

//  FUNCTIONS

func int
    while 1
        _num = input()
        
        if subtypeOf _num === "int"
            return _num
        end if
    end while
end func

func main
    if nrows(argv) >= 3
        expectExcept("1 argument(s), got {}" format (nrows(argv) - 1))
    end if

    port = 8080

    if nrows(argv) === 2
        _port = (argv row 1) slice 1
        
        if subtypeOf _port !== "int"
            typeExcept(subtypeOf _port)
        end if
        
        if _port < 0
            rangeExcept(string(_port))
        end if
        
        port = _port
    end if

    user = ""

    while 1
        echo "------ MUSIC LIBRARY ------\n"
        echo "1\t-\tLibrary\n"
        echo "2\t-\tArtists\n"
        echo "3\t-\tAlbums\n"
        echo "4\t-\tSongs\n"
        echo ">\t"

        num = 0

        while 1
            num = int()
            
            if num >= 1 && num < 5
                break
            end if
        end while
        
        echo "\n"
        
        req = "/api/"
        
        if num === 1
            req += "getLibrary"
        
        else if num === 2
            echo "------ MUSIC LIBRARY ------\n"
            echo "1\t-\tGet Artists\n"
            echo "2\t-\tFind Artists\n"
            echo "3\t-\tAdd Artist\n"
            echo "4\t-\tRemove Artist\n"
            echo ">\t"
            
            while 1
                num = int()
            
                if num < 0 || (num >= 1 && num < 5)
                    break
                end if
            end while
            
            echo "\n"
            
            if num < 0
                continue
            end if
            
            if num === 1
                req += "getArtists"
            
            else if num === 2
                echo "Pattern:\t"
                
                pattern = input()
                
                echo "\n"
                
                req += "findArtists?pattern={}" format pattern
            else if num === 3
                echo "Artist Name:\t"
                
                artistName = string(input() ?? "")
                
                echo "\n"
                
                req += "addArtist?artistName={}" format artistName
            else
                echo "Artist ID:\t"
                
                artistId = 0
                while (artistId = int()) < 1
                    continue
                end while
                    
                echo "\n"
                
                req += "removeArtist?artistId={}" format artistId
            end if
        else if num === 3
            echo "------ MUSIC LIBRARY ------\n"
            echo "1\t-\tGet Albums\n"
            echo "2\t-\tFind Albums\n"
            echo "3\t-\tAdd Album\n"
            echo "4\t-\tRemove Album\n"
            echo ">\t"
            
            while 1
                num = int()
            
                if num < 0 || (num >= 1 && num < 5)
                    break
                end if
            end while
            
            echo "\n"
            
            if num < 0
                continue
            end if
            
            if num === 1
                req += "getAlbums"
                
            else if num === 2
                echo "Pattern:\t"
                
                pattern = input()
                
                echo "\n"
                
                req += "findAlbums?pattern={}" format pattern
            else if num === 3
                echo "Artist ID:\t"
                
                artistId = 0
                while (artistId = int()) < 1
                    continue
                end while
                
                echo "Album Year:\t"
                
                albumYear = 0
                while (albumYear = int()) < 1970
                    continue
                end while
                
                echo "Album Name:\t"
                
                albumName = string(input() ?? "")
                
                echo "\n"
                
                req += "addAlbum?artistId={}&albumYear={}&albumName={}" format array(artistId, albumYear, albumName)
            else
                echo "Album ID:\t"
                
                albumId = 0
                while (albumId = int()) < 1
                    continue
                end while
                    
                echo "\n"
                
                req += "removeAlbum?albumId={}" format albumId
            end if
        else
            echo "------ MUSIC LIBRARY ------\n"
            echo "1\t-\tGet Songs\n"
            echo "2\t-\tFind Songs\n"
            echo "3\t-\tAdd Song\n"
            echo "4\t-\tRemove Song\n"
            echo "5\t-\tPlay Song\n"
            echo "6\t-\tClear Plays\n"
            echo ">\t"
            
            while 1
                num = int()
            
                if num < 0 || (num >= 1 && num < 7)
                    break
                end if
            end while
            
            echo "\n"
            
            if num < 0
                continue
            end if
            
            if num === 1
                req += "getSongs"
            else if num === 2
                echo "Pattern:\t"
                
                pattern = input()
                
                echo "\n"
                
                req += "findSongs?pattern={}" format pattern
            else if num === 3
                echo "Album ID:\t"
                
                albumId = 0
                while (albumId = int()) < 1
                    continue
                end while
                
                echo "Position:\t"
                
                position = -1
                while ((position = int()) < 0)
                    continue
                end while
                
                echo "Song Title:\t"
                
                songTitle = string(input() ?? "")
                
                echo "\n"
                
                req += "addSong?albumId={}&position={}&songTitle={}" format array(albumId, position, songTitle)
            else if num === 4
                echo "Song ID:\t"
                
                songId = 0
                while (songId = int()) < 1
                    continue
                end while
                    
                echo "\n"
                
                req += "removeSong?songId={}" format songId
            else if num === 5
                echo "Song ID:\t"
                
                songId = 0
                while (songId = int()) < 1
                    continue
                end while
                    
                echo "\n"
                
                req += "playSong?songId={}" format songId
            else
                echo "Song ID:\t"
                
                songId = 0
                while (songId = int()) < 1
                    continue
                end while
                    
                echo "\n"
                
                req += "clearPlays?songId={}" format songId
            end if
        end if
        
        sw = start()
        sfd = connectClient(port)
        
        if sfd === -1
            continue
        end if

        send(sfd, req)

        array res = recv(sfd, 30)

        if !res
            echo "Connection to the server timed out.\n\n"
            closeSocket(sfd)
            continue
        end if
        
        dif = precision(stop(sw), 2)
        res = parseTable(res, PATTERN)
        
        if (res col 1) === "error"
            echo "ERROR:\t{}\n" format (res col 2)
        else
            for i = 0, i < (count res - 1) / first res, i += 1
                for j = 1, j <= (res cell i, 0), j += 1
                    echo "{}\t" format (res cell i, j)
                end for
                
                echo "\n"
            end for
        end if
        
        echo "\n"
        
        num = (count res - 1) / first res
        
        echo (count res - 1) / first res === 1 ? "No results", (string(num - 1) + " Result") + (num === 2 ? "", "s")
        echo " ({} s)\n" format padDecimal(dif, 2)
        echo "\n"
        echo "Export? (Y/n):\t"
        
        ans = string(input() ?? "")
        
        echo "\n"
        
        if count ans === 1 && toUpper ans === "Y"
            filename = "/Users/{}/Downloads/Export" format USER
            ext = ".csv"
        
            if exists(filename + ext)
                i = 1
                while exists((filename + " ({})" format i) + ext)
                    i += 1
                end while
                
                write((filename + " ({})" format i) + ext, res, PATTERN)
            else
                write(filename + ext, res, PATTERN)
            end if
        end if
        
        closeSocket(sfd)
    end while
end func

main()
