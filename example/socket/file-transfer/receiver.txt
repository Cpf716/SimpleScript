//  Author: Corey Ferguson
//  Date:   September 22, 2023
//  File:   receiver.txt
//

include("@/array/table/parseTable")
include((dir = "/Library/Application Support/SimpleScript/example/socket/") + "util/client.txt", connectClient)
include(dir + "config.txt")
include("@/string")

config()

//  FUNCTIONS

func main
    if nrows(argv) >= 3
        expectExcept("1 argument(s), got {}" format (nrows(argv) - 1))
    end if

    port = 8080

    if nrows(argv) === 2
        _port = (argv row 1) slice 1
        
        if subtypeOf _port !== "int"
            typeExcept(subtypeOf _port)
        end if
        
        if _port < 0
            rangeExcept(string(_port))
        end if
        
        port = _port
    end if

    sfd = connectClient(port)
    
    if sfd === -1
        return
    end if

    array req = recv(sfd, 30)
    
    if !req
        echo "Connection to the server timed out.\n"
        closeSocket(sfd)
        return
    end if
    
    req = parseTable(req)

    for i = 0, i < (count req - 1) / first req, i += 1
        for j = 1, j <= (req cell i, 0), j += 1
            echo "{}\t" format (req cell i, j)
        end for
        
        echo "\n"
    end for
    
    echo "\n"
    echo "Export? (Y/n):\t"
    
    ans = string(input() ?? "")
    
    if count ans === 1 && toUpper ans === "Y"
        filename = "/Users/{}/Downloads/Export" format USER
        ext = ".csv"
    
        if exists(filename + ext)
            i = 1
            while exists((filename + " ({})" format i) + ext)
                i += 1
            end while
            
            write((filename + " ({})" format i) + ext, req, PATTERN)
        else
            write(filename + ext, req, PATTERN)
        end if
    end if
    
    return ""
end func

return main()
