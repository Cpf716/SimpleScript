//  Author: Corey Ferguson
//  Date:   September 22, 2023
//  File:   receiver.txt
//

include((dir = "@/datetime/") + "parseTime")
include(dir + "isoString")
include("/Library/Application Support/SimpleScript/example/socket/util/client.txt", connectClient)

func main
    if nrows(argv) >= 3
        expectExcept("1 argument(s), got {}" format (nrows(argv) - 1))
    end if

    port = 8080

    if nrows(argv) === 2
        _port = (argv row 1) slice 1
        
        if subtypeOf _port !== "int"
            typeExcept(subtypeOf _port)
        end if
        
        if _port < 0
            rangeExcept(string(_port))
        end if
        
        port = _port
    end if

    while 1
        sfd = connectClient(port)
        
        if sfd === -1
            return
        end if
        
        req = null

        while 1
            if !(req = recv(sfd, 30))
                break
            end if
            
            array arr = split(req, "\n")
            
            for str in arr
                echo "{}\n" format isoString(parseTime(str))
            end for
        end while
        
        closeSocket(sfd)
        
        echo "\n"
    end while
end func

main()
