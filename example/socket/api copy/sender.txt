//  Author: Corey Ferguson
//  Date:   September 24, 2023
//  File:   apiServer.txt
//

include("@/datetime/parseTime")
include("@/table/encode")

func `query`(sql)
    con = -1

    try
        con = connect("tcp://localhost:3306", "root", "root")
        
        setSchema(con, "music_library")
    catch err
        echo "SQL: {}\n" format err

        return
    end try
    
    echo "{}\n" format sql
    
    array `res` = null

    try
        `res` = query(con, sql)
    catch err
        echo "SQL: {}\n" format err
    end try
    
    try
        close(con)
    catch err
        echo "SQL: {}\n" format err
    end try
    
    `res` = encode(`res`)
    num = parseTime(gmt())
    path = "/tmp/{}.csv" format num

    write(path, `res`, "csv")
    
    `res` = read(path)
    
    remove(path)
    
    return `res`
end func

func main
    timeout = 0

    sfd = server(8080, 1024)

    echo "Server listening on port 8080...\n"

    while 1
        array conv = null
        if !(conv = poll(sfd))
            continue
        end if
        
        echo "Connected...\n"
        
        cfd = first conv
        res = recv(cfd)
        
        req = null
        
        if res === "/api/artists"
            req = `query`("select * from artists")

        else if res === "/api/albums"
            req = `query`("select * from albums")
            
        else if res === "/api/songs"
            req = `query`("select * from songs")
            
        else if res === "/api/library"
            req = `query`("select artist_name, album_year, album_name, song_title from songs join albums on songs.album_id = albums.album_id join artists on albums.artist_id = artists.artist_id")
        else
            req = undefined
        end if
        
        send(cfd, req)
        
        close(cfd)
        
        echo "Disconnected.\n"
    end while
end func

main()
