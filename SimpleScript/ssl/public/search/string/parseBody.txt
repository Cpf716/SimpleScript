//  Author: Corey Ferguson
//  Date:   December 19, 2023
//  File:   parseBody.txt
//

include("@/array/table/parseTable")
include((dir = "@/search/string/") + "split")
include(dir + "trim")

if typeOf SEP === undefined
    define SEP ","
end if

func main()
    if nrows(argv) !== 1
        expectExcept("1 argument(s), got {}" format nrows(argv))
    end if
    
    req = (argv row 0) slice 1
    
    if typeOf req !== "string"
        typeExcept(typeOf req, "string")
    end if
    
    if req === null
        throw "null"
    end if

    array body = split(req, "?")
    
    if count body >= 3
        return 1
    end if
    
    array params = (count body === 1 ? null : last body)
    
    body = array(2, "url", first body)
    ncols = 1
    
    if first params !== null
        params = split(first params, "&")
    
        for param in params
            array pair = split(param, "=")
            array val = ""
            
            if count pair !== 2
                return 1
            end if
            
            if count last pair
                val = parseTable(last pair, SEP) slice 2
            end if
            
            if count val > ncols
                ncols = count val
            end if
            
            pair = array(count val + 1, first pair) + val
            body += pair
        end for
        
        //  normalize
        for i = 0; i <= count params; i += 1
            j = body.(i * (ncols + 2))
            
            for k = 0; k <= ncols - j; k += 1
                body insert i * (ncols + 2) + j + k + 1, null
            end for
        end for
    end if
    
    body insert 0, ncols + 2
    
    return body
end func

return main()
