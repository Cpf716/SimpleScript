//  Author: Corey Ferguson
//  Date:   August 3, 2023
//  File:   split.txt
//

include("@/exception/expectExcept")

func main()
    if nrows(argv) !== 2
        expectExcept("2 argument(s), got {}" format nrows(argv))
    end if

    text = ((argv row 0) slice 1, (argv cell 0, 0))

    if typeOf text !== "string"
        typeExcept(typeOf text, "string")
    end if

    if text === null
        throw "null"
    end if

    pat = ((argv row 1) slice 1, (argv cell 1, 0))

    if typeOf pat !== "string"
        typeExcept(typeOf pat, "string")
    end if

    if pat === null
        throw "null"
    end if

    array arr = null, num = -1

    beg = 0, end = 0
    while end <= count text - count pat
        i = 0
        while i < count pat && text.(end + i) === pat.i
            i += 1
        end while
        
        if i === count pat
            arr.(num += 1) = (text substr beg, end)
            beg = (end += i)
        else
            end += 1
        end if
    end while

    arr.(num += 1) = text substr beg

    return arr
end func

return main()
