//  Author: Corey Ferguson
//  Date:   August 3, 2023
//  File:   sortCols.txt
//

include("@/exception/rangeExcept")

func main()
    if nrows(argv) !== 2
        expectExcept("2 argument(s), got {}" format nrows(argv))
    end if

    array arr = ((argv row 0) slice 1, (argv cell 0, 0))

    if subtypeOf arr !== "table"
        typeExcept(subtypeOf arr, "table")
    end if

    idx = ((argv row 1) slice 1, (argv cell 1, 0))

    if subtypeOf idx !== "integer"
        typeExcept(subtypeOf idx, "integer")
    end if

    if idx < 0 || idx >= (count arr - 1) / first arr
        rangeExcept("idx {}, nrows {}" format array(idx, (count arr - 1) / first arr))
    end if

    for i = 1; i < first arr; i += 1
        for j = i - 1; j >= 0 && (arr cell idx, j) > (arr cell idx, j + 1); j -= 1
            for k = 0; k < (count arr - 1) / first arr; k += 1
                tmp = (arr cell k, j)
                
                arr.(k * first arr + j + 1) = (arr cell k, j + 1)
                arr.(k * first arr  + j + 2) = tmp
            end for
        end for
    end for

    return arr
end func

return main()
