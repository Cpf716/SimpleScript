//  Author: Corey Ferguson
//  Date:   August 24, 2023
//  File:   day.txt
//

include((dir = "@/datetime/") + "constants/months")
include(dir + "date/isLeapYear")
include("@/search/string/padStart")

func main()
    if nrows(argv) !== 1
        expectException("Expected 1 argument(s), got {}" format nrows(argv))
    end if

    timestamp = (argv row 0) slice 1

    if subtypeOf timestamp !== "integer"
        typeException(subtypeOf timestamp, "integer")
    end if

    if timestamp < 0
        rangeExcept(string(timestamp))
    end if

    seconds = 0, secondsInYear = 0, year = 1970
    while timestamp >= seconds + (secondsInYear = (365 + isLeapYear(year)) * 86400)
        seconds += secondsInYear
        year += 1
    end while

    timestamp -= seconds

    seconds = 0, secondsInMonth = 0, month = 0
    while timestamp >= seconds + (secondsInMonth = (months().(month * 2 + 1) + (month === 1 && isLeapYear(year))) * 86400)
        seconds += secondsInMonth
        month += 1
    end while

    timestamp -= seconds

    `day` = 0
    while timestamp >= (`day` + 1) * 86400
        `day` += 1
    end while

    return `day` + 1
end func

return main()


