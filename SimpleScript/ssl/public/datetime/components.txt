//  Author: Corey Ferguson
//  Date:   December 15, 2023
//  File:   components.txt
//

include("@/datetime/data/months")
include("@/datetime/date/isLeapYear")
include("@/string")

func main
    if nrows(argv) < 2
        expectExcept("1 argument(s), got {}" format (nrows(argv) - 1))
    end if
    
    if nrows(argv) >= 4
        expectExcept("2 argument(s), got {}" format (nrows(argv) - 1))
    end if

    timestamp = (argv row 1) slice 1

    if subtypeOf timestamp !== "int"
        typeExcept(subtypeOf timestamp, "int")
    end if

    if timestamp < 0
        rangeExcept(string(timestamp))
    end if
    
    flag = 1
    
    if nrows(argv) === 3
        `flag` = (argv row 2) slice 1

        if subtypeOf `flag` !== "int"
            typeExcept(subtypeOf `flag`, "int")
        end if
        
        flag = `flag`
    end if
    
    //  years

    seconds = 0, secondsInYear = 0, year = 1970
    while timestamp >= seconds + (secondsInYear = (365 + isLeapYear(year)) * 86400)
        seconds += secondsInYear
        year += 1
    end while

    timestamp -= seconds
    
    str."year" = 0
    
    year -= 1970
    
    if year
        str."year" = year
    end if
    
    //  months

    seconds = 0, secondsInMonth = 0, `months` = 0
    while timestamp >= seconds + (secondsInMonth = (months().(`months` * 2 + 1) + (`months` === 1 && isLeapYear(year))) * 86400)
        seconds += secondsInMonth
        `months` += 1
    end while

    timestamp -= seconds
    
    if `months`
        str."month" = `months`
    end if
    
    //  weeks
    
    if flag
        weeks = 0
        while timestamp >= (weeks + 1) * 604800
            weeks += 1
        end while
        
        timestamp -= 604800 * weeks
        
        if weeks
            str."week" = weeks
        end if
    end if
    
    //  days

    days = 0
    while timestamp >= (days + 1) * 86400
        days += 1
    end while

    timestamp -= 86400 * days
    
    if days
        str."day" = days
    end if

    days += 1
    
    //  hours

    hours = 0
    while timestamp >= (hours + 1) * 3600
        hours += 1
    end while

    timestamp -= hours * 3600
    
    if hours
        str."hour" = hours
    end if
    
    //  minutes

    minutes = 0
    while timestamp >= (minutes + 1) * 60
        minutes += 1
    end while

    timestamp -= minutes * 60
    
    if minutes
        str."minute" = minutes
    end if

    seconds = timestamp
    
    if seconds
        str."second" = seconds
    end if
    
    return str
end func

return main()

