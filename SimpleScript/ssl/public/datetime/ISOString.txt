//  Author: Corey Ferguson
//  Date:   August 24, 2023
//  File:   ISOString.txt
//

include((dir = "@/datetime/") + "constants/months")
include(dir + "date/isLeapYear")
include("@/search/string/padStart")

func main()
    if nrows(argv) !== 1
        expectException("Expected 1 argument(s), got {}" format nrows(argv))
    end if

    timestamp = (argv row 0) slice 1

    if subtypeOf timestamp !== "integer"
        typeException(subtypeOf timestamp, "integer")
    end if

    if timestamp < 0
        rangeExcept(string(timestamp))
    end if

    seconds = 0, secondsInYear = 0, year = 1970
    while timestamp >= seconds + (secondsInYear = (365 + isLeapYear(year)) * 86400)
        seconds += secondsInYear
        year += 1
    end while

    timestamp -= seconds

    seconds = 0, secondsInMonth = 0, month = 0
    while timestamp >= seconds + (secondsInMonth = (months().(month * 2 + 1) + (month === 1 && isLeapYear(year))) * 86400)
        seconds += secondsInMonth
        month += 1
    end while

    timestamp -= seconds

    day = 0
    while timestamp >= (day + 1) * 86400
        day += 1
    end while

    timestamp -= 86400 * day

    hours = 0
    while timestamp >= (hours + 1) * 3600
        hours += 1
    end while

    timestamp -= hours * 3600

    minutes = 0
    while timestamp >= (minutes + 1) * 60
        minutes += 1
    end while

    timestamp -= minutes * 60
    seconds = timestamp
    
    date = array(year, month + 1, day + 1)

    for i = 0; i < 2; i += 1
        date.(i + 1) = padStart(string(date.(i + 1)), 2, "0")
    end for
    
    date = date join "-"
    time = (array(hours, minutes, seconds) map val, padStart(string(val), 2, "0")) join ":"
    
    return array(date, time) join "T"
end func

return main()


